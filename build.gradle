buildscript {
    repositories {
        mavenCentral()
        maven {
            name 'minecraftplus'
            url 'https://maven.minecraftplus.org/releases'
            mavenContent {
                releasesOnly()
            }
        }
        maven {
            name 'minecraftplus-snapshots'
            url 'https://maven.minecraftplus.org/snapshots/'
            mavenContent {
                snapshotsOnly()
            }
        }
        maven { url = 'https://maven.minecraftforge.net/' }
    }
    dependencies {
        classpath  group: 'net.minecraftforge.gradleutils', name: 'GradleUtils', version: '2.+', changing: true
    }
}

plugins {
    id 'java-library'
    id 'eclipse'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}
apply plugin: 'net.minecraftforge.gradleutils'

group 'org.minecraftplus'
version = gradleutils.getTagOffsetBranchVersion(null, 'master', 'HEAD')
println('Version: ' + version)

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava.options.encoding = 'UTF-8'

ext {
    CHANGELOG = rootProject.file('build/changelog.txt')
    REPO = 'FARTPlus'
    TITLE = 'Forge Auto Renaming Tool Plus'
    MANIFEST = manifest{
        attributes('Main-Class': 'net.minecraftforge.fart.Main')
        attributes([
            'Specification-Title':      'FARTPlus',
            'Specification-Vendor':     'Forge Development LLC',
            'Specification-Version':    gradleutils.gitInfo.tag,
            'Implementation-Title':     'SimpleInstaller',
            'Implementation-Version':   project.version,
            'Implementation-Vendor':    'Forge Development LLC'
        ] as LinkedHashMap, 'net/minecraftforge/fart/')
    }
    INITIAL_CHANGELOG_TAG = '0.1'
}

repositories {
    mavenCentral()
    maven {
        name 'minecraftplus'
        url 'https://maven.minecraftplus.org/releases'
        mavenContent {
            releasesOnly()
        }
    }
    maven {
        name 'minecraftplus-snapshots'
        url 'https://maven.minecraftplus.org/snapshots/'
        mavenContent {
            snapshotsOnly()
        }
    }
    maven { url = 'https://maven.minecraftforge.net/' }
}

test {
    useJUnitPlatform()
}

license {
    header = file('LICENSE-header.txt')
    ext.project = TITLE
}

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
    api 'net.minecraftforge:srgutils:0.4.9'
    implementation 'org.ow2.asm:asm:9.2'
    implementation 'org.ow2.asm:asm-commons:9.2'
    implementation 'org.ow2.asm:asm-tree:9.2'
    
    testImplementation(platform('org.junit:junit-bom:5.8.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.powermock:powermock-core:2.0.9')
    
    compileOnly('com.google.code.findbugs:jsr305:3.0.2')
}


test {
  useJUnitPlatform()
  testLogging {
    events 'passed', 'skipped', 'failed'
  }
}

archivesBaseName = project.name.toLowerCase()

java.withSourcesJar()

shadowJar {
    manifest.from(MANIFEST)
    minimize()

    def relocations = [
            'org.objectweb.asm',
            'net.minecraftforge.srgutils',
            'joptsimple'
    ]

    relocations.forEach {
        relocate it, "net.minecraftforge.fart.relocated.$it"
    }
}

assemble.dependsOn shadowJar

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archivesBaseName
            from components.java
            pom {
                name = TITLE
                description = 'A tool that renames java bytecode elements.'
                url = "https://github.com/MinecraftPlus/${REPO}"
                scm {
                    url = "https://github.com/MinecraftPlus/${REPO}"
                    connection = "scm:git:git://github.com/MinecraftPlus/${REPO}.git"
                    developerConnection = "scm:git:git@github.com:MinecraftPlus/${REPO}.git"
                }
                issueManagement {
                    system = 'github'
                    url = "https://github.com/MinecraftPlus/${REPO}/issues"
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'LexManos'
                        name = 'LexManos'
                    }
                    developer {
                        id = 'theWituch'
                        name = 'theWituch'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "minecraftplus"
            url = "https://maven.minecraftplus.org"
            allowInsecureProtocol = false

            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = url.toString().concat("/snapshots")
            } else {
                url = url.toString().concat("/releases")
            }

            // To know how use credentials see https://docs.gradle.org/current/samples/sample_publishing_credentials.html
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

changelog {
    fromTag INITIAL_CHANGELOG_TAG
}